import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

android {
    defaultConfig {
        applicationId "com.osfans.mcpdict"
        minSdkVersion 24
        targetSdkVersion 34
        versionCode 97
        versionName "5.7"
    }

    signingConfigs {
        release {
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources false
            proguardFiles 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            def stringsFile = android.sourceSets.main.res.sourceFiles.find { it.name.equals 'strings.xml' }
            def appName = new XmlParser().parse(stringsFile).string.find { it.@name.equals 'app_name' }.text()
            def versionName = new SimpleDateFormat ("yyyyMMdd").format(new Date())
            outputFileName = "${appName}${versionName}.APK"
        }
    }
    
    def propsFile = rootProject.file('keystore.properties') //store release config in local.properties
    def configName = 'release'

    if (propsFile.exists() && android.signingConfigs.hasProperty(configName)) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        if (props!=null && props.containsKey('storeFile')) {
            android.signingConfigs.named(configName) {
                storeFile = rootProject.file(props['storeFile'])
                storePassword = props['storePassword']
                keyAlias = props['keyAlias']
                keyPassword = props['keyPassword']
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    namespace 'com.osfans.mcpdict'
    buildFeatures {
        buildConfig true
    }
    compileSdk 35
    buildToolsVersion '35.0.0'
}

dependencies {
    implementation 'com.readystatesoftware.sqliteasset:sqliteassethelper:2.0.1'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.viewpager2:viewpager2:1.1.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'
    implementation 'androidx.webkit:webkit:1.12.1'
    implementation 'com.github.MKergall:osmbonuspack:6.9.0'
    // Fix Duplicate class
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.8.22"))
}
